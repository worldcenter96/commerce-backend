plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.sonarqube" version "6.0.1.5171"
}

bootJar {
    enabled = false
}

jar {
    enabled = false
}

repositories {
    mavenCentral()
}

sonar {
    properties {
        property "sonar.projectKey", "impostor-commerce_impostor-commerce-backend"
        property "sonar.organization", "impostor-commerce"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    group = 'com.sparta'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.sonarqube'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Log Json Parsing
        implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

        // Redisson
        implementation 'org.redisson:redisson-spring-boot-starter:3.39.0'

        // Spring Security
        implementation 'org.springframework.security:spring-security-crypto'

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.mysql:mysql-connector-j'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.mockito:mockito-core:4.6.1'

        // QueryDSL
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

        // Redis 세션 저장소
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.session:spring-session-data-redis'

        // AWS
        implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':admin') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
    }
}

project(':b2b') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}

project(':b2c') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
        testImplementation 'com.h2database:h2'
    }
}
